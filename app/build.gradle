plugins {
    id 'com.android.application'
}

android {
    namespace 'net.dancier.webview'
    compileSdk 32

    def commitCountOut = new StringBuilder(), commitCountError = new StringBuilder()
    def proc = 'git rev-list --first-parent --count origin/main'.execute()
    proc.consumeProcessOutput(commitCountOut, commitCountError)
    proc.waitForOrKill(1000)
    println "git commit count: ${commitCountOut}, git commit error: ${commitCountError}"

    def code = commitCountOut.toString().toInteger()
    def minVersion = code % 1000
    def majVersion = ((code - minVersion)/1000) + 1

    def vName = "version ${majVersion}.${minVersion}"

    println "build new version, name: ${vName}"

    defaultConfig {
        applicationId "net.dancier.webview"
        minSdk 21
        targetSdk 32
        versionCode code
        versionName vName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("release-key.jks")
            keyAlias System.getenv("RELEASE_KEY_ALIAS")
            storePassword System.getenv("RELEASE_STORE_PASSWORD")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}